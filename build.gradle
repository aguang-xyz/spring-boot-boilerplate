buildscript {
  ext {
    springBootVersion = '2.1.6.RELEASE'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'xya.aguang'
version = '1.0.0.SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
  mavenCentral()
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-web')
  testCompile('org.springframework.boot:spring-boot-starter-test')
}

task dockerBuild {
  dependsOn bootJar

  doLast {
    def dockerfile = file('./build/tmp/Dockerfile')

    dockerfile.text  = 'FROM openjdk:11-slim\n'
    dockerfile.text += 'WORKDIR /workspace\n'
    dockerfile.text += "COPY build/libs/${rootProject.name}-${version}.jar ./\n"
    dockerfile.text += "CMD [\"java\", \"-jar\", \"${rootProject.name}-${version}.jar\"]"

    exec {
      commandLine 'docker', 'build', '.', '-f', 'build/tmp/Dockerfile', '-t', "${rootProject.name}:${version}"
    }
  }
}

task dockerRun {
  dependsOn dockerBuild

  doLast {
    exec {
      commandLine 'docker', 'run', "${rootProject.name}:${version}"
    }
  }
}
